# Grammar for Alpha

## Permanents
permanentCard -> ability? (newline ability)*

ability -> keywords
        -> staticAbility
        -> activatedAbility
        -> triggeredAbility

keywords -> keyword ((semicolon|comma) space keyword)*

activatedAbility -> costs colon space effect period (restriction period)?

triggeredAbility -> triggerCondition (comma space subordinateClause)? comma space effect (space subordinateClause)? period

staticAbility -> staticAbility space staticAbility
              -> subordinateClause comma space staticAbility
              -> replacementEffect period
              -> continuous period
              -> damage verbIs ' dealt to ' object ' instead' period
              -> player ' may have ' object ' enter ' zone ' as a copy of any ' object ' on ' zone (space prepositionalPhrase)? (period)?
              -> player ' may spend ' color ' mana as though it were ' color ' mana' period
              -> player ' may play any number of lands on each of your turns' period
              -> player ' don’t lose the game for having 0 or less life' period
              -> player ' can’t untap more than one ' object ' during ' phase period
              -> 'Activated abilities of ' object ' cost ' costs ' more to activate' period
              -> 'For each ' damage ' that would be dealt to ' object comma space 'if' space condition comma space effect period

replacementEffect -> subordinateClause comma space effect
                  -> object space objectVerbPhrase prepositionalPhrase
                  -> subordinateClause comma space 'instead' space effect
                  -> subordinateClause comma space effect space 'instead'
                  -> subordinateClause comma space 'you may ' costs ' instead' period space subordinateClause comma space effect
                  -> subordinateClause comma space 'discard it' comma space 'but you may put it on top of ' zone ' instead of into ' zone

## Spells

spellCard -> spellAbility (\n spellAbility)*

modal -> 'Choose one ' dash (newline bullet space effect period)+

spellAbility -> effect period
             -> restriction period
             -> modal
             -> player ' chooses a number of lands they control equal to ' amount ', then sacrifices the rest. Players discard cards and sacrifice creatures the same way.'
             -> 'At the beginning of ' phase ', destroy ' object '. Ignore this effect for each creature the player didn’t control continuously since the beginning of the turn.'
             -> duration comma space 'instead of declaring blockers, each defending player chooses any number of creatures they control and divides them into a number of piles equal to the number of attacking creatures for whom that player is the defending player. Creatures ' player ' control that can block additional creatures may likewise be put into additional piles. Assign each pile to a different one of those attacking creatures at random. Each creature in a pile that can block the creature that pile is assigned to does so.'
             -> duration comma space 'any time you could activate a mana ability, you may ' costs period space subordinateClause comma space effect

## Effects

continuous -> continuous (space duration)?
           -> object space objectVerbPhrase
           -> object (saxon)? 's power and toughness are each equal to ' amount
           -> player ' control ' object
           -> player ' have no maximum hand size'
           -> player ' skip ' phase

oneShot -> playerVerbPhrase (' and sacrifice ' object ' of an opponent’s choice')? (' and ' player ' loses all unspent mana')? (period space player ' may attach ' object ' to ' object ' of their choice')?
	-> player space playerVerbPhrase
        -> object ' become unblocked'
	-> object ' deals ' damage ' to ' something
        -> object ' deals ' damage ' to ' something ' and ' damage ' to ' something
        -> object ' deals ' damage ' to ' something ' and ' object ' gains "Enchanted creature loses flying"'
        -> object ' deals ' damage ' to ' something comma space 'where X is ' amount
        -> object ' deals ' damage ' to ' something period space player ' gain life equal to the damage dealt, but not more life than the player’s life total before the damage was dealt, the planeswalker’s loyalty before the damage was dealt, or the creature’s toughness'
        -> object ' deals ' damage ' to ' something '. If ' condition ', it can’t be regenerated this turn, and if it would die this turn, exile it instead'
        -> object ' deals ' damage ' divided evenly, rounded down, among any number of targets'
        -> object ' deals damage to ' something ' equal to ' amount
        -> object ' deals damage equal to ' amount ' to ' something

effect -> effect (period)? space conjunction space effect
       -> (effect comma space)? effect comma space conjunction space effect
       -> oneShot
       -> continuous
       -> 'Prevent ' damage (period space 'Until end of turn, you may ' costs ' any time you could cast an instant' period space subordinateClause comma space effect)?
       -> 'prevent that ' damage
       -> 'The next time a ' color ' source of your choice would deal damage to you this turn, prevent that damage'
       -> 'The next time a source of your choice would deal damage to you this turn, prevent that damage. You gain life equal to the damage prevented this way'
       -> 'The next time a source of your choice would deal damage to target creature this turn, that source deals that damage to you instead'
       -> 'The next ' number ' damage that would be dealt to ' object ' this turn is dealt to its owner instead. Only ' object '’s owner may activate this ability'
       -> player ' loses half their life, rounded up'
       -> 'Choose ' object period space object ' attacks this turn if able. Destroy it at the beginning of the next end step if it didn’t attack this turn'
       -> 'If Chaos Orb is on the battlefield, flip Chaos Orb onto the battlefield from a height of at least one foot. If Chaos Orb turns over completely at least once during the flip, destroy all nontoken permanents it touches. Then destroy Chaos Orb'
       -> 'at the beginning of each of your upkeeps for the rest of the game, remove all mire counters from a land that a mire counter was put onto with Cyclopean Tomb but that a mire counter has not been removed from with Cyclopean Tomb'
       -> 'The next time ' object ' would deal combat damage to you this turn, prevent all but 1 of that damage'
       -> player ' antes the top card of ' zone
       -> player ' may ' costs period space subordinateClause comma space effect
       -> player ' may ' effect
       -> player ' may tap or untap ' object
       -> player ' shuffles their hand and graveyard into ' zone
       -> 'until your next turn, ' player ' can’t be attacked except by creatures with flying and/or islandwalk'
       -> player ' may pay any amount of mana. ' object ' deals 2 damage to that player. Prevent X of that damage, where X is the amount of mana that player paid this way'
       -> player ' may choose a creature card in your hand whose mana cost could be paid by some amount of, or all of, the mana you spent on {X}. If you do, you may cast that card face down as a 2/2 creature spell without paying its mana cost. If the creature that spell becomes as it resolves has not been turned face up and would assign or deal damage, be dealt damage, or become tapped, instead it’s turned face up and assigns or deals damage, is dealt damage, or becomes tapped. Activate only as a sorcery.'
       -> player ' divides all creatures without flying they control into a “left” pile and a “right” pile. Then, for each attacking creature you control, choose “left” or “right.” That creature can’t be blocked this combat except by creatures with flying and creatures in a pile with the chosen label'
       -> 'it loses “enchant creature card in a graveyard” and gains “enchant creature put onto the battlefield with ~.” Return enchanted creature card to the battlefield under your control and attach ~ to it. When ~ leaves the battlefield, that creature’s controller sacrifices it'
       -> 'remove' space object space 'from combat'
       -> player ' may have ' object ' block ' object ' of your choice'
       -> player ' activates a mana ability of ' object
       -> player ' loses all unspent mana and ' player ' add the mana lost this way'
       -> 'Look at ' zone ' and choose a card from it'
       -> player ' control ' player ' until ' object ' finishes resolving'
       -> 'The player plays that card if able. While doing so, the player can activate mana abilities only if they’re from lands that player controls and only if mana they produce is spent to activate other mana abilities of lands the player controls and/or to play that card. If the chosen card is cast as a spell, you control the player while that spell is resolving'

damage -> number ' damage'
       -> 'all combat damage that would be dealt' space duration
       -> 'all damage that would be dealt to ' player ' by ' object
       -> article space 'next ' damage ' that would be dealt to ' something space duration

## Definitions

adjective -> 'enchanted'
          -> 'tapped'
	  -> 'unblocked'
          -> 'blocking'
          -> 'attacking'
          -> 'sacrificed'
          -> 'token'
          -> 'additional'

amount -> 'half ' amount comma ' rounded ' ('up'|'down')
       -> 'up to ' amount
       -> englishNumber (space conjunction space 'more')?
       -> object saxon space characteristic
       -> 'X'
       -> article space 'number of ' object (' minus ' number)?
       -> article space 'damage dealt to ' player ' this turn'
       -> playerPossessive ' life total'
       -> 'that many'
       -> 'any number of'

characteristic -> 'power'
               -> 'toughness'
               -> 'mana value'

condition -> object (space)? verbIs space ('un')? 'tapped'
          -> object (space)? verbIs space prepositionalPhrase (space prepositionalPhrase)?
          -> object (space)? verbIs space object
          -> object (space)? verbIs space 'attacking'
          -> object ' has a ' counter ' counter on it'
          -> object ' has ' keyword
          -> object ' wasn’t the first land you played this turn'
          -> object ' attacked or blocked this combat'

costs -> ('pay' space)? cost (comma space costs)?

cost ->(mana)+
     -> lBracket tap rBracket
     -> number space 'life'
     -> 'remove' space article space counter ' counter from ' object
     -> 'sacrifice' space object
     -> 'skip' space demonstrative space ('turn'|'draw')

counter -> statMod
        -> counterType

determiner -> article
           -> demonstrative
           -> amount

duration -> 'until end of turn'
         -> 'this turn'
         -> 'until ' object ' leaves ' zone
         -> 'until end of combat'
         -> 'each combat'

keyword -> 'enchant' object
        -> 'banding'
        -> 'first' space 'strike'
        -> 'flying'
        -> 'vigilance'
        -> 'defender'
        -> 'protection from' space color
        -> landType 'walk'
        -> 'indestructible'
        -> 'fear'
        -> 'haste'
        -> 'trample'
        -> 'reach'

mana -> (lBracket (manaLetter | number) rBracket)+

object -> premodifier space object
       -> object space postmodifier
       -> object plural
       -> (object comma space)* object space conjunction object
       -> tilde
       -> type
       -> 'card'
       -> 'spell'
       -> 'permanent'
       -> 'it'
       -> article ' top card of ' zone

objectVerbPhrase -> verbIs space 'dealt damage'
                 -> verbIs space 'tapped for mana'
                 -> verbIs space 'put into ' zone ' from ' zone
                 -> verbIs space object
                 -> 'blocks or becomes blocked by ' object
                 -> 'deals damage to ' player
                 -> objectVerb space zone 'tapped'
                 -> objectVerb space color
                 -> objectVerb space zone
                 -> objectVerb space object (' if able')?
                 -> 'do' ('es')? 'n’t untap during ' phase
                 -> 'do so'
                 -> 'cost' space costs space 'more to cast'
             	 -> 'gains ' keyword space duration period space 'Destroy ' object ' at the beginning of the next end step'
		 -> 'gains ' keyword
		 -> 'gains trample and gets ' statMod space duration ', where X is ' amount '. At the beginning of the next end step, destroy that creature if it attacked this turn'
		 -> ' get' ('s')? ' ' statMod (' as long as you control a Swamp')?
		 -> ' gets ' statMod space duration '. If this ability has been activated four or more times this turn, sacrifice ' object ' at the beginning of the next end step'
		 -> ' get' ('s')? ' ' statMod ' and ha' ('s'|'ve') space ability
		 -> ' ha' ('s'|'ve') space quotedAbility
		 -> ' ha' ('s '|'ve ') ability (' and can’t be enchanted by ' object)? ('. this effect doesn't remove ' object)?
		 -> ' can attack as though ' object ' didn't have defender'
		 -> ' can attack as though ' object ' had haste'
		 -> ' can’t attack' (' ' subordinateClause)?
		 -> ' can block ' object
		 -> ' can’t be blocked'
		 -> ' can’t be blocked ' ('except ')? 'by ' object
		 -> ' can’t block ' object

objectVerb -> rawObjectVerb (plural)?

postmodifier -> player ' control' (plural)?
             -> player ' controlled at the beginning of this turn'
             -> 'controlled by the player who controls the fewest'
             -> prepositionalPhrase
             -> 'named Plague Rats' space prepositionalPhrase
	     -> 'able to block ' object
             -> 'that ' verbIs ' still ' object
             -> 'other than ' object
             -> 'that didn’t attack this turn'
             -> object ' was blocking that had become blocked by ' object ' this combat'
             -> 'put ' prepositionalPhrase ' this way'
             -> 'dealt damage by ' object ' this turn'
	     -> player ' has controlled continuously since the beginning of the turn'
	     -> 'of your choice'

premodifier -> (premodifier comma space)+ premodifier
            -> (negation (space|dash))? adjective
            -> (negation (space|dash))? color
            -> determiner
            -> type
            -> number slash number

prepositionalPhrase -> prepositionalPhrase space prepositionalPhrase
                    -> preposition space zone
                    -> preposition space object
                    -> preposition space quality
                    -> preposition space amount space counter ' counters on ' object

phase -> playerPossessive space 'turn'
      -> playerPossessive space rawPhase (plural)?
      -> article space rawPhase ' of ' object saxon ' controller'
      -> article space rawPhase
      -> article space 'opponent’s turn' comma ' before attackers are declared'
      -> article space 'next' space rawPhase
      -> 'combat'
      -> 'combat before blockers are declared'

player -> determiner space player
       -> player plural
       -> object saxon space 'controller'
       -> object saxon space 'owner'
       -> 'player'
       -> 'you'
       -> 'opponent'
       -> 'active player'
       -> 'defending player'
       -> 'they'

playerPossessive -> 'your'
                 -> 'their'
                 -> player saxon
                 -> 'their controller' plural saxon
                 -> 'its controller' saxon
                 -> 'the chosen player' saxon

playerVerbPhrase -> playerVerb
                 -> playerVerb space object (space 'at random')?
		 -> playerVerb space player
                 -> playerVerb space object space 'for mana'
                 -> '’re dealt damage'
                 -> costs
                 -> playerVerb mana
                 -> playerVerb space article space 'amount of ' mana ' equal to ' amount
                 -> playerVerb space amount space 'mana of any ' ('one ')? 'color'
		 -> playerVerb space amount space 'mana of any type that land produced'
		 -> playerVerb space article space 'additional ' mana
		 -> playerVerb space article space 'text of ' object ' by replacing all instances of one ' textAspect ' with another'
		 -> playerVerb space article space textAspect
		 -> 'copy' space object comma space except period space player space 'may choose new targets for the copy'
		 -> 'counter' space object (' unless its controller pays {X}. If that player doesn’t, they tap all lands with mana abilities they control and lose all unspent mana')
		 -> 'create a 1/1 colorless Insect artifact creature token with flying named Wasp'
		 -> playerVerb space object (period space ('It'|'They') 'can’t be regenerated')?
		 -> 'destroy ' object period space object ' deals damage to each creature and each player equal to ' amount
		 -> 'destroy ' object ' at end of combat'
		 -> playerVerb space 'their hand'
		 -> playerVerb space 'your hand'
		 -> 'exchange ' object ' with ' object
		 -> 'exile ' object period space player ' gains life equal to ' amount
		 -> 'gain' (plural)? space number ' life'
		 -> 'gain life equal to ' amount period space object ' deals damage to ' something ' equal to ' amount '.'
		 -> 'Look at the top ' amount ' cards of ' zone ', then put them back in any order. ' player ' may have ' player ' shuffle'
		 -> 'Look' space 'at' space zone
		 -> 'lose the game'
		 -> 'lose life equal to ' amount
		 -> 'put' space object space prepositionalPhrase
		 -> 'put' space amount space counter ' counter on ' object (' for each creature that died this turn')?
		 -> 'put' space amount space counter ' counter on ' object '. That land is a Swamp for as long as it has a ' counter ' counter on it'
		 -> 'put' space amount space counter ' counters on ' object '. This ability can’t cause the total number of ' counter ' counters on ' object ' to be greater than ' amount
		 -> 'remove a ' counter ' counter from ' object
		 -> 'return' space object (' from ' zone)? ' to ' zone period
		 -> 'sacrifice ' object (subordinateClause)?
		 -> 'sacrifice ' object period space subordinateClause comma space effect
		 -> 'search ' zone space prepositionalPhrase
		 -> 'shuffle'
		 -> 'take an extra turn after this one'

playerVerb -> rawPlayerVerb (plural)?

quality -> 'mana value' space number
        -> 'power' space number space conjunction space comparator
        -> 'toughness ' comparator ' than ' amount
        -> 'power and toughness each equal to ' amount
        -> keyword

quotedAbility -> openQuote ability closeQuote

rawPhase -> turnPart
         -> (turnPart space)? turnPart space step

restriction -> 'Cast ' object ' only during ' phase
            -> 'Cast ' object ' only before ' phase
            -> 'Spend only ' color ' mana on X'
            -> 'As an additional cost to cast this spell, ' costs
            -> 'This spell costs ' costs ' more to cast for each target beyond the first'
            -> 'Activate only during ' phase ('and only once each turn')?
	    -> 'Activate only during an opponent’s turn, before attackers are declared'
            -> 'Remove ' object ' from your deck before playing if you’re not playing for ante'

something -> determiner something
          -> object
          -> player
          -> determiner space target
          -> something space conjunction space something

statMod -> plusMinus number slash plusMinus number (' where X is ' amount (', and Y is ' amount)?)?

subordinateClause -> subordinateConjunction space 'long' space subordinateClause
                  -> subordinateConjunction space condition
                  -> subordinateConjunction space player space playerVerbPhrase
                  -> subordinateConjunction space player ' would draw ' object ' during ' phase
                  -> subordinateConjunction space player ' would gain life'
                  -> subordinateConjunction space player ' would begin your turn while ' object ' is tapped'
                  -> subordinateConjunction space object space objectVerbPhrase
		  -> subordinateConjunction space 'an effect causes you to discard a card'
                  -> subordinateConjunction space 'it’s ' object ' in addition to its other types'
                  -> subordinateConjunction space 'it doesn’t copy that creature’s color and it has “At the beginning of your upkeep, you may have this creature become a copy of target creature, except it doesn’t copy that creature’s color and it has this ability.”'
                  -> subordinateConjunction space 'that the copy is ' color

textAspect -> 'color word'
           -> 'basic land type'

triggerCondition -> triggerWord space triggerEvent

triggerEvent -> 'the beginning of' space phase
             -> 'end of combat'
             -> player (space)? playerVerbPhrase
             -> object space objectVerbPhrase

type -> negation (dash)? type
     -> cardType
     -> landType
     -> creatureType
     -> enchantmentType
     -> 'chosen type'

verbIs -> rawVerbIs (negation)?

zone -> playerPossessive space rawZone
     -> article space rawZone
     
## Morphemes

article -> 'the'
        -> 'a'
        -> 'an'

cardType -> 'creature'
         -> 'land'
         -> 'artifact'
         -> 'enchantment'

color -> 'white'
      -> 'blue'
      -> 'black'
      -> 'green'
      -> 'red'

comparator -> 'less'
           -> 'greater'

conjunction -> 'and'
            -> 'or'
	    -> 'then'

counterType -> 'corpse'
            -> 'vitality'
            -> 'mire'

creatureType -> 'Goblin'
             -> 'Golem'
             -> 'Merfolk'
             -> 'Wall'
             -> 'Zombie'

demonstrative -> 'this'
              -> 'these'
              -> 'those'
              -> 'that'

englishNumber -> 'one'
              -> 'two'
              -> 'three'
              -> 'seven'

enchantmentType -> 'aura'

landType -> 'plains'
         -> 'island'
         -> 'swamp'
         -> 'mountain'
         -> 'forest'

preposition -> 'with' ('out')?
            -> 'in' ('to')?
            -> 'on' ('to')?
            -> 'above'

plusMinus -> plus
          -> dash

rawDeterminer -> 'all'
              -> 'each'
              -> 'any'
              -> 'other'
              -> 'target'
              -> 'no'
              -> 'only'

rawObjectVerb -> 'attack'
              -> 'become'
              -> 'block'
              -> 'die'
              -> 'enter'

rawPlayerVerb -> 'add'
	      -> 'ante'
	      -> 'can’t'
	      -> 'cast'
	      -> 'change'
	      -> 'control'
	      -> 'destroy'
	      -> 'discard'
	      -> 'do'
	      -> 'draw'
	      -> 'own'
	      -> 'play'
	      -> 'regenerate'
	      -> ('un')? 'tap'

rawZone -> 'graveyard'
        -> 'battlefield'
        -> 'hand'
        -> 'library'
        -> 'ante'

subordinateConjunction -> 'as'
                       -> 'if'
                       -> 'unless'
                       -> 'except'

turnPart -> 'untap'
         -> 'upkeep'
         -> 'draw'
         -> 'declare'
         -> 'attackers'
         -> 'blockers'
         -> 'combat'
         -> 'damage'
         -> 'end'

triggerWord -> 'at'
            -> 'when'
            -> 'whenever'

## individual words

rawVerbIs -> 'is'
          -> 'are'
          -> apostrophe letterS

step -> 'step'

## Lexemes

letterS -> 's'

manaLetter -> 'W'
           -> 'U'
           -> 'B'
           -> 'R'
           -> 'G'
           -> 'C'

negation -> 'non'
         -> 'un'
	 -> 'n’t'

number -> '0'
       -> '1'
       -> '2'
       -> '3'
       -> '4'
       -> '5'
       -> '6'
       -> '7'
       -> 'X'

plural -> letterS

openQuote -> '“'
closeQuote -> '”'

saxon -> (apostrophe (letterS)?|letterS)

tap -> 't'

## Symbols

apostrophe -> '’'

bullet -> '•'

comma -> ','

colon -> ':'

dash -> '-'
     -> '—'

lBracket -> '{'

newline -> '\n'

period -> '.'

plus -> '+'

rBracket -> '}'

semicolon -> ';'

slash -> '/'

space -> ' '

tilde -> '~'
